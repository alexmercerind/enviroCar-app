import com.hierynomus.gradle.license.tasks.LicenseFormat

apply plugin: 'com.android.application'

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
}

android {
    namespace "org.envirocar.obd"
    ndkVersion rootProject.ext.ndkVersion

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    defaultConfig {
        minSdk rootProject.ext.minSdk
        compileSdk rootProject.ext.compileSdk
        targetSdk rootProject.ext.targetSdk
    }
}

// custom licenseFormat-tasks to access the directories, which are not part of the standard maven directory pattern
tasks.register('licenseFormatSrc', LicenseFormat) {
    source = fileTree(dir: "src").include("**/*.java", '**/*.xml', '**/*.html').exclude('**/MoreObjects.java', '**/Preconditions.java')
}
licenseFormat.dependsOn licenseFormatSrc

tasks.register('licenseFormatRes', LicenseFormat) {
    source = fileTree(dir: "res").include("**/*.java", '**/*.xml', '**/*.html').exclude('**/MoreObjects.java', '**/Preconditions.java')
}
licenseFormat.dependsOn licenseFormatRes
